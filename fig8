go atlas

mesh space.mult=1.0

x.mesh loc=0.00  spacing=0.1
x.mesh loc=1.15 spacing=0.01
x.mesh loc=1.5 spacing=0.02
x.mesh loc=1.85 spacing=0.01
x.mesh loc=3.00  spacing=0.1

y.mesh loc=-0.017 spacing=0.02
y.mesh loc=0.000  spacing=0.01
y.mesh loc=0.1 spacing=0.01
y.mesh loc=0.200  spacing=0.02
y.mesh loc=0.600  spacing=0.02
y.mesh loc=1.800  spacing=0.1

region num=1 y.max=0 material=SiO2
region num=2 y.min=0 y.max=0.2 material=Silicon
region num=3 y.min=0.2 y.max=0.6 material=SiO2
region num=4 y.min=0.6 y.max=1.8 material=Silicon

electrode name=gate      x.min=1.0 x.max=2.0 y.min=-0.017 y.max=-0.017
electrode name=source    x.min=0.0 x.max=0.5 y.min=0.0    y.max=0.0
electrode name=drain     x.min=2.5 x.max=3.0 y.min=0.0    y.max=0.0
electrode name=substrate x.min=0.0 x.max=3.0 y.min=1.8    y.max=1.8

doping uniform p.type conc=1e17 region=2
doping uniform p.type conc=1e17 region=4
doping gaussian n.type conc=1e20 characteristic=0.2 region=2 x.left=0.0 x.right=1.0 y.top=0.0 ratio.lateral=0.1
doping gaussian n.type conc=1e20 characteristic=0.2 region=2 x.left=2.0 x.right=3.0 y.top=0.0 ratio.lateral=0.1

save outf=soi_mos.str
tonyplot soi_mos.str

contact name=gate workfunction=4.15

models cvt srh auger bgn fldmob conmob print

method newton trap
solve init

log outf=ids_vgs.log master
solve name=drain vdrain=0.1
solve name=gate  vgate=0.0
solve name=gate  vstep=0.05 vfinal=1.6

extract init infile="ids_vgs.log"
extract name="Vth_gm" ( xintercept( maxslope( curve( abs(v."gate"), abs(i."drain") ) ) ) - abs(ave(v."drain"))/2 )

tonyplot ids_vgs.log
quit
